plugins {
    id "com.jfrog.bintray" version "1.8.0"
    id "com.github.spotbugs" version "1.6.1"
}

group 'codes.brick'
version '1.1.2'

apply plugin: 'java'
apply plugin: "checkstyle"
apply plugin: "pmd"
apply plugin: "maven-publish"

sourceCompatibility = 1.9

repositories {
    jcenter()
}

ext {
    gdxVersion = '1.9.8'
    ashleyVersion = '1.7.3'
}

repositories {
    mavenCentral()
}

dependencies {
}

checkstyle {
    configFile = new File(rootDir, "config/checkstyle/google_checks.xml")
    toolVersion = '8.8'
    ignoreFailures = true
}

spotbugs {
    toolVersion = '3.1.2'
    ignoreFailures = true
}

pmd {
    toolVersion = '6.1.0'
    ignoreFailures = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled false
    }
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled true
        html.enabled false
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-controllers:$gdxVersion"
    compile "com.badlogicgames.ashley:ashley:$ashleyVersion"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'codes.brick'
        name = 'common'
        userOrg = 'brickcodes'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/brick-codes/libgdx-common.git'
        version {
            name = project.version
            released  = new Date()
            vcsTag = project.version
        }
    }
    publish = true
    publications = ["CommonPublication"]
}

def pomConfig = {
    licenses {
        license {
            name "MIT"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "DenialAdams"
            name "Richard McCormack"
            email "brick@brick.codes"
        }
    }
    scm {
       url "https://github.com/brick-codes/libgdx-common"
    }
}

publishing {
    publications {
        CommonPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'codes.brick'
            artifactId 'common'
            version project.version
            pom.withXml {
              def root = asNode()
              root.appendNode('description', 'utility code for libgdx games')
              root.appendNode('name', 'libgdx-common')
              root.appendNode('url', 'http://brick.codes')
              root.children().last() + pomConfig
            }
        }
    }
}

